group 'dvekeman'
version '0.0.1-SNAPSHOT'

buildscript {
    repositories {
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-js-wrappers"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.31"
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.3.31"
        classpath "com.github.node-gradle:gradle-node-plugin:1.3.0"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
    }
}

repositories {
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-js-wrappers"
        }
}

apply plugin: 'kotlin2js'
apply plugin: 'kotlinx-serialization'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:1.3.31"
    compile "org.jetbrains.kotlin:kotlin-compiler-embeddable:1.3.31"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:0.6.12"

    // compile project(":kotlin-extensions")
    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:0.11.0"
}

def projectName = name

compileKotlin2Js {
    kotlinOptions {
        outputFile = "$projectDir/build/classes/main/${projectName}.js"
        moduleKind = "commonjs"
        sourceMap = true
        sourceMapEmbedSources = "always"
    }
}

ext.configurePublishing = { baseVersion ->
    apply plugin: 'com.github.node-gradle.node'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'
    apply plugin: 'java'

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    def v = "$baseVersion-kotlin-1.3.31"
    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publish = true
        pkg {
            repo = 'kotlin-typings'
            name = projectName
            userOrg = 'dvekeman'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/dvekeman/kotlin-typings.git'
            version {
                name = v
            }
        }
        publications = ['Publication']
    }

    publishing {
        publications {
            Publication(MavenPublication) {
                from components.java
                groupId 'dvekeman'
                artifactId projectName
                version v

                artifact sourcesJar
            }
        }
    }
}

ext.configurePublishingWithNPM = { baseVersion ->
    configurePublishing(baseVersion)

    task processPkg(type: Copy) {
        from '.'
        into 'build/npm'
        include 'package.json'
    }

    task prepublish(type: Copy) {
        from '.'
        into 'build/npm'
        exclude 'package.json'
        exclude 'build/npm'
    }

    npm_publish {
        args = ['--access', 'public']
        execOverrides {
            it.workingDir = 'build/npm'
        }
    }

    npm_publish.dependsOn prepublish
    npm_publish.dependsOn processPkg
    prepublish.dependsOn build
}

configurePublishingWithNPM("0.0.1-pre.1")

